# randomized checks use index numbers (generated at runtime) to look up info in
# a table of {treasure id, treasure param, collect mode, destination player}.
# bit 7 of a treasure id (for creating interaction 60s or calling giveTreasure)
# indicates that treasure info should be overwritten by an indexed lookup when
# set. bits 0-6 of the id are treated as the low 7 bits of the index; the subid
# is used as the higher bits if present.

common:
  # b and c are used to index the table.
  # returns the address of the entry in hl.
  # returns c if the check was looked up.
  # also sets wNetIndexOut.
  00/lookupCheck: |
      xor a # reset carry
      bit 7,b
      ret z
      push de
      ld e,06
      ld hl,lookupCheck_body
      call interBankCall
      pop de
      scf
      ret

  06/lookupCheck_body: |
      call convertIdsToIndex
      ld hl,wNetIndexOut
      ld (hl),b
      inc hl
      ld (hl),c
      ld hl,checkIndexTable
      add hl,bc
      add hl,bc
      add hl,bc
      add hl,bc
      ret

  # converts b = id, c = subid to bc = index.
  06/convertIdsToIndex: |
      ld a,b
      ld b,c
      ld c,a
      sra b
      ret c
      res 7,c
      ret

  # b and c are used to index the table.
  # returns a, b, and c, and dest player, id, and param.
  # returns nz if the check was looked up.
  00/getCheckInfo: |
      push de
      ld e,06
      ld hl,getCheckInfo_body
      call interBankCall
      ld a,e
      cp a,ff
      pop de
      ret

  06/getCheckInfo_body: |
      push bc
      call lookupCheck
      pop bc
      ld e,ff
      ret nc
      ld b,(hl)
      inc hl
      ld c,(hl)
      inc hl
      inc hl
      ld e,(hl)
      ret
